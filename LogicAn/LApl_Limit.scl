FUNCTION_BLOCK "LApl_Limit"
{ S7_Optimized_Access := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      In : "LApl_AnaVal";
      OutHyst : Real;
      OutHiLim : Real := 100.0;
      OutLoLim : Real;
      Init : Bool;
      Feature : DWord := 2#00000000_00000000_00000000_00000000;
   END_VAR

   VAR_OUTPUT 
      Out : "LApl_AnaVal";
      OutHiAct : Bool;
      OutLoAct : Bool;
      ErrorNum : Int := -1;
   END_VAR


BEGIN
	// качество сигнала
	#Out.ST := #In.ST;
	
	// проверяем правильность введенных настроек
	IF #OutHiLim < #OutLoLim THEN
	    #OutHiLim := #OutLoLim;
	ELSIF (#OutHiLim - #OutLoLim) < #OutHyst THEN
	    #OutHyst := (#OutHiLim - #OutLoLim);
	END_IF;
	
	// инициализация
	IF #Init THEN
	    #OutHiAct := false;
	    #OutLoAct := false;
	END_IF;
	
	IF #In.Value <= #OutLoLim THEN
	    // ниже нижнего предела
	    #Out.Value := #OutLoLim;
	    #OutLoAct := true;
	    #OutHiAct := false;
	ELSIF #In.Value >= #OutHiLim THEN
	    // выше верхнего предела
	    #Out.Value := #OutHiLim;
	    #OutLoAct := false;
	    #OutHiAct := true;
	ELSE
	    // между нижним и верхним пределом
	    #Out.Value := #In.Value;
	    IF #OutLoAct AND (#In.Value > #OutLoLim + #OutHyst) THEN
	        #OutLoAct := false;
	    END_IF;
	    IF #OutHiAct AND (#In.Value < #OutHiLim - #OutHyst) THEN
	        #OutHiAct := false;
	    END_IF;
	END_IF;
	    
END_FUNCTION_BLOCK


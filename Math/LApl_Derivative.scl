FUNCTION_BLOCK "LApl_Derivative"
{ S7_Optimized_Access := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      In : "LApl_AnaVal";
      OutHiLim : Real := 100.0;
      OutLoLim : Real;
      TD : Time := T#1s;
      SampleTime : Time := T#100ms;
      LagTime : Time := T#10s;
      Feature : DWord := 2#00000000_00000000_00000000_00000000;
   END_VAR

   VAR_OUTPUT 
      Out : "LApl_AnaVal";
      OutHiAct : Bool;
      OutLoAct : Bool;
      ErrorNum : Int := -1;
   END_VAR

   VAR 
      stat : Struct
         Out : Real;
         PrevIn : Real;
      END_STRUCT;
   END_VAR


BEGIN
	// статус сигнала
	#Out.ST := #In.ST;
	
	// расчет производной
	#stat.PrevIn := #stat.PrevIn + #stat.Out * DINT_TO_REAL(#SampleTime) / DINT_TO_REAL(#TD);
	#stat.Out := (#In.Value - #stat.PrevIn) * DINT_TO_REAL(#TD) / (DINT_TO_REAL(#LagTime + #SampleTime / 2));
	
	// ограничение выхода
	#OutHiAct := false;
	#OutLoAct := false;
	IF #stat.Out > #OutHiLim THEN
	    #Out.Value := #OutHiLim;
	    #OutHiAct := true;
	ELSIF #stat.Out < #OutLoLim THEN
	    #Out.Value := #OutLoLim;
	    #OutLoAct := true;
	ELSE
	    #Out.Value := #stat.Out;
	END_IF;
END_FUNCTION_BLOCK

